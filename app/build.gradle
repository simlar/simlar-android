apply plugin: 'com.android.application'

def getGitVersion = { ->
    try {
        return "git -C ${rootDir} describe --tags --always --dirty".execute().text.trim()
    }
    catch (ignored) {
        return "git-not-found";
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "org.simlar"
        minSdkVersion 9
        targetSdkVersion 23
        versionName getGitVersion()
    }

    productFlavors {
        push {
            versionName = android.defaultConfig.versionName
            buildConfigField("boolean", "IS_GCM_ENABLED", "true")
        }

        alwaysOnline {
            versionName = android.defaultConfig.versionName + " (alwaysOnline)"
            buildConfigField("boolean", "IS_GCM_ENABLED", "false")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        checkAllWarnings true
        disable 'UnusedIds', 'IconExpectedSize', 'GoogleAppIndexingWarning'
        warningsAsErrors true
    }
}

dependencies {
    pushCompile 'com.google.android.gms:play-services-gcm:8.3.0'
    compile files('libs/liblinphone.jar')
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.googlecode.libphonenumber:libphonenumber:7.2.2'
    compile 'com.android.support:support-v4:23.1.1'
}
