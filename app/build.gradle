apply plugin: 'com.android.application'

def getGitVersion = { ->
    try {
        return "git -C ${rootDir} describe --tags --always --dirty".execute().text.trim()
    }
    catch (ignored) {
        return "git-not-found";
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "org.simlar"
        minSdkVersion 14
        targetSdkVersion 25
        versionName getGitVersion()
    }

    productFlavors {
        push {
            versionName = android.defaultConfig.versionName
            buildConfigField("boolean", "IS_GCM_ENABLED", "true")
        }

        alwaysOnline {
            versionName = android.defaultConfig.versionName + " (alwaysOnline)"
            buildConfigField("boolean", "IS_GCM_ENABLED", "false")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        checkAllWarnings true
        disable 'GoogleAppIndexingWarning'
        warningsAsErrors true
    }
}

dependencies {
    pushCompile 'com.google.android.gms:play-services-gcm:10.2.6'
    compile files('libs/liblinphone.jar')
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.googlecode.libphonenumber:libphonenumber:8.5.0'
    compile 'com.android.support:support-v4:25.3.1'
}
